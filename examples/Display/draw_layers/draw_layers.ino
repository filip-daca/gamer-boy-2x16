#include <GamerBoy2x16.h>

#define DELAY 100
#define CAR_DELAY 2

#define X_MAX 16*5

GamerBoy2x16 gb = GamerBoy2x16();

byte roadSprites[3][8] = {
  { 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x1F, 0x1F },
  { 0x00, 0x08, 0x0A, 0x0C, 0x08, 0x08, 0x1F, 0x1F },
  { 0x00, 0x00, 0x00, 0x00, 0x00, 0x14, 0x1F, 0x1F }
};

byte carSprites[9][8] = {
  { 0x00, 0x00, 0x10, 0x10, 0x10, 0x00, 0x00, 0x00 }, 
  { 0x00, 0x10, 0x08, 0x18, 0x18, 0x10, 0x00, 0x00 },  
  { 0x00, 0x18, 0x04, 0x1C, 0x1C, 0x08, 0x00, 0x00 }, 
  { 0x00, 0x0C, 0x12, 0x1E, 0x1E, 0x14, 0x00, 0x00 },
  { 0x00, 0x06, 0x09, 0x1F, 0x1F, 0x0A, 0x00, 0x00 },
  { 0x00, 0x03, 0x04, 0x0F, 0x0F, 0x05, 0x00, 0x00 },
  { 0x00, 0x01, 0x02, 0x07, 0x07, 0x02, 0x00, 0x00 },
  { 0x00, 0x00, 0x01, 0x03, 0x03, 0x01, 0x00, 0x00 },
  { 0x00, 0x00, 0x00, 0x01, 0x01, 0x00, 0x00, 0x00 }
};

byte sunSprite[8] = { 0x00, 0x0E, 0x11, 0x11, 0x11, 0x0E, 0x00, 0x00 };

byte birdsSprites[2][8] = {
  { 0x00, 0x00, 0x00, 0x0A, 0x04, 0x00, 0x00, 0x00 },
  { 0x00, 0x00, 0x0D, 0x02, 0x00, 0x00, 0x00, 0x00 }
};

byte road[16];
byte sky[15];

byte carX = X_MAX;
byte carDelay = CAR_DELAY;

void setup() {
  gb.initialize();

  designSky();
  designRoad();
}

void loop() {
  gb.update();

  gb.display.clear();

  moveCar();

  drawSky();
  drawSun();
  drawRoad();
  drawCar();

  gb.display.flush();
  
  delay(DELAY);
}

void designSky() {
  for (byte x = 0; x < 15; ++x) {
    if (random(0, 3) == 0) {
      sky[x] = random(0, 2) + 1;
    }
  } 
}

void drawSky() {
  for (byte x = 0; x < 15; ++x) {
    if (sky[x] != 0) {
      gb.display.draw(birdsSprites[sky[x] - 1], x, 0);
    }
  } 
}

void drawSun() {
  gb.display.draw(sunSprite, 15, 0);
}

void designRoad() {
  for (byte x = 0; x < 16; ++x) {
    road[x] = random(0, 3);
  }
}

void drawRoad() {
  for (byte x = 0; x < 16; ++x) {
    gb.display.draw(roadSprites[road[x]], x, 1);
  }
}

void drawCar() {
  if (carX % 5 == 0) {
    gb.display.draw(carSprites[4], carX / 5, 1);
  } else {
    gb.display.draw(carSprites[carX % 5 - 1], carX / 5 + 1, 1);
    gb.display.draw(carSprites[carX % 5 + 4], carX / 5, 1);
  }
}

void moveCar() {
  carDelay--;
  if (carDelay <= 0) {
    carDelay = CAR_DELAY;
    carX--;
    if (carX <= 0) {
      carX = X_MAX;
    }
  }
}
